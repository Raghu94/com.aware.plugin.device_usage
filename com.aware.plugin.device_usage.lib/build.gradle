apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = version_readable
group = "com.awareframework"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    enforceUniquePackageName = false

    defaultConfig {
        versionCode version_code
        versionName version_readable
        targetSdkVersion 24
        minSdkVersion 10 //Android 2.3.3
    }

    signingConfigs {
        release {
            //set values in ~/.gradle/gradle.properties to sign with your own keystore and certificate
            storeFile file(project.hasProperty("KEYSTORE_FILE") ? KEYSTORE_FILE : "/")
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
            keyAlias project.hasProperty("KEY") ? KEY : ""
            keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : ""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

bintray {
    user = project.hasProperty("bintrayUsername") ? bintrayUsername : ""
    key = project.hasProperty("bintrayKey") ? bintrayKey : ""

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'com.awareframework'
        name = 'com.aware.plugin.device_usage'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/denzilferreira/com.aware.plugin.device_usage' //EDIT ME
        issueTrackerUrl = 'https://github.com/denzilferreira/com.aware.plugin.device_usage/issues' //EDIT ME
        labels = ['aar','android','framework','aware']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/denzilferreira/com.aware.plugin.device_usage.git' //EDIT ME
        desc = "AWARE Plugin: Device Usage - library" //EDIT ME
        userOrg = user
        githubRepo = "denzilferreira/com.aware.plugin.device_usage" //EDIT ME

        version {
            name = "$version_readable"
            desc = bintray.pkg.desc
            released = new Date()
            vcsTag = "$version_readable"
        }
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

repositories {
    maven { url "https://jitpack.io" } //MPAndroidChart
    maven { url  "http://dl.bintray.com/jfrog/bintray-tools" }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
//    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.3'
    compile "com.android.support:appcompat-v7:${support_libs}"
    compile 'com.github.denzilferreira:aware-client:development-SNAPSHOT'
//    compile "com.awareframework:aware-core:${aware_libs}@aar"
//    compile (name: "aware-core-release", ext: "aar")
//    compile "com.koushikdutta.ion:ion:2.1.6"
//    compile "org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.0.2"
}
